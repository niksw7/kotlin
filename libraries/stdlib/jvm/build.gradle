description = 'Kotlin Standard Library for JVM'

apply plugin: 'kotlin-platform-jvm'

archivesBaseName = 'kotlin-stdlib'

configureJvm6Project(project)
configureDist(project)
configurePublishing(project)

project.ext["jpsLibraryPath"] = rootProject.distLibDir

sourceSets {
    annotations {
        if(!System.properties.'idea.active') {
            java {
                srcDir "${rootDir}/core/runtime.jvm/src"
                include 'org/jetbrains/annotations/**'
            }
            kotlin {
                exclude '**/*'
            }
        }
    }
    builtins {
        if(!System.properties.'idea.active')
        java {
            srcDir "${rootDir}/core/builtins/src"
            srcDir 'runtime'
        }
    }
    main {
        java {
            srcDir "${rootDir}/core/builtins/src"
            srcDir 'runtime'
            srcDir 'src'
        }
    }
    experimental {
        kotlin {
            srcDir "../experimental"
        }
    }
    test {
        kotlin {
            srcDir 'test'
        }
    }
}

dependencies {
    expectedBy project(":kotlin-stdlib-common")

    builtinsCompile group: 'org.jetbrains', name: 'annotations', version:'13.0'
    compile group: 'org.jetbrains', name: 'annotations', version:'13.0'
    compile fileTree("${rootDir}/dist/builtins")

    testCompile project(':kotlin-test:kotlin-test-junit')
}

configurations {
    builtins
}

jar {
    manifestAttributes(manifest, project, 'Main')
    from("${rootDir}/dist/builtins")
    from sourceSets.experimental.output
}

task distJar(type: Jar) {
    baseName = 'dist-kotlin-stdlib'
    version = null
    manifestAttributes(manifest, project, 'Main')
    from("${rootDir}/dist/builtins")
    from sourceSets.annotations.output
    from sourceSets.main.output
    from sourceSets.experimental.output
}

task builtinsJar(type: Jar) {
    baseName = "builtins"
    from("${rootDir}/dist/builtins")
    from sourceSets.builtins.output
}

sourcesJar {
    from "${rootDir}/core/builtins/native"
}

artifacts {
    archives sourcesJar
    archives javadocJar
    builtins builtinsJar
}

dist {
    [distJar, sourcesJar].forEach {
        from(it) {
            rename('dist-', '')
        }
        // legacy
        from(it) {
            rename('dist-', '')
            rename('kotlin-stdlib', 'kotlin-runtime')
        }
    }
    from (configurations.compile) {
        include 'annotations*.jar'
    }
}

task dexMethodCount(type: DexMethodCount) {
    from jar
    ownPackages = ['kotlin']
}
check.dependsOn(dexMethodCount)

compileBuiltinsKotlin {
    dependsOn ":core:builtins:serialize"
    kotlinOptions {
        freeCompilerArgs = [
                "-version",
                "-Xallow-kotlin-package",
                "-Xmulti-platform",
                "-cp", "${rootDir}/dist/builtins",
                "-module-name", "kotlin-runtime"
        ]
    }
}
compileKotlin {
    dependsOn(":core:builtins:serialize")
    dependsOn(":prepare:build.version:writeStdlibVersion")
    dependsOn(":kotlin-stdlib-common:preprocessSourcesMain")
    kotlinOptions {
        freeCompilerArgs = [
                "-version",
                "-Xallow-kotlin-package",
                "-Xmultifile-parts-inherit",
                "-Xmulti-platform",
                "-Xnormalize-constructor-calls=enable",
                "-Xdump-declarations-to=${buildDir}/stdlib-declarations.json",
                "-module-name", "kotlin-stdlib"
        ]
    }
}

compileTestKotlin {
    dependsOn(":kotlin-stdlib-common:preprocessSourcesTest")
}


compileExperimentalKotlin {
    kotlinOptions {
        languageVersion = "1.3"
        apiVersion = "1.3"
        freeCompilerArgs = [
                "-version",
                "-Xallow-kotlin-package",
                "-Xmultifile-parts-inherit",
                "-Xdump-declarations-to=${buildDir}/stdlib-experimental-declarations.json",
                "-module-name", "kotlin-stdlib-experimental"
        ]
    }
}

kotlin.experimental.coroutines "enable"
